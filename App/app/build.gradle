plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

repositories {
    mavenCentral()
}

def loadLocalProperties() {
    // Android Studio creates a local.properties file in the root directory of
    // the project. Since this gradle file is in the default app module, we
    // need to look in the parent directory for the local.properties file.
    def f = file("../local.properties")
    def properties = new Properties()
    if (f.canRead()) {
        f.newReader().withReader {
            properties.load(it)
        }
    }
    return properties
}

def localProperties = loadLocalProperties()

android {
    compileSdkVersion 28
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    defaultConfig {
        applicationId "com.company.esp32.alerts"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName "2.1.1"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (localProperties.getProperty(
                    "autosignReleaseWithDebugKey", "false").toBoolean()) {
                signingConfig signingConfigs.debug
            }
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:multidex:1.0.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Ktor
    def ktor_version = "1.6.1"
    implementation "io.ktor:ktor:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-gson:$ktor_version"

// Dependency Injection
    implementation "io.insert-koin:koin-ktor:3.1.2"

// Test Ktor
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "io.mockk:mockk:1.12.0"}
